apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set("sources")
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {

    publishing {
        publications {
            library(MavenPublication) {
                groupId project.CCL_GROUP_ID
                artifactId project.CCL_ARTIFACT_ID
                version project.CCL_VERSION
                artifact androidSourcesJar
                artifact bundleReleaseAar

                pom {
                    name = 'creditor-commerce-library-android'
                    description = 'The Pay by Bank app Merchant Library for Android makes it easy for you to add Pay by Bank app as a new payment methodThe Creditor Commerce Android Library (PBARFP) that enables a Creditor to enable payments through a consumer selected debtor participant on a single device with universal/app link.'

                    url = 'https://github.com/Mastercard/creditor-commerce-library-android'
                    organization {
                        name = 'Mastercard Zapp'
                        url = 'https://mastercard.com'
                    }
                    licenses {
                        license {
                            name = 'Apache License, Version 2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0'
                        }
                    }
                    developers {
                        developer {
                            name = 'Mastercard Zapp'
                        }
                    }
                    scm {
                        url = 'https://github.com/Mastercard/creditor-commerce-library-android'
                        connection = 'scm:git:git://github.com:Mastercard/creditor-commerce-library-android'
                        developerConnection = 'scm:git:ssh://git@github.github.com:Mastercard/creditor-commerce-library-android'
                    }

                    withXml {
                        def dependenciesNode = asNode().appendNode('dependencies')

                        project.configurations.implementation.allDependencies.each {
                            if (it.name != 'unspecified') {
                                def dependencyNode = dependenciesNode.appendNode('dependency')
                                dependencyNode.appendNode('groupId', it.group)
                                dependencyNode.appendNode('artifactId', it.name)
                                dependencyNode.appendNode('version', it.version)
                            }
                        }
                    }
                }
            }
        }

        repositories {
            // The repository to publish to, Sonatype/MavenCentral
            maven {
                // This is an arbitrary name, you may also use "mavencentral" or
                // any other name that's descriptive for you
                name = "sonatype"
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

                credentials {
                    username ossrhUsername
                    password ossrhPassword
                }
            }
        }
    }

    signing {
        sign publishing.publications
        sign configurations.archives
    }

}