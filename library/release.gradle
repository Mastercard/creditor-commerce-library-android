// ./gradlew clean build :creditor-merchant-library:generateLibraryRelease
apply plugin: 'maven-publish'

def localReleaseDest = "${buildDir}/release/${version}"

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            localLibraryRelease(MavenPublication){
                groupId project.CCL_GROUP_ID
                artifactId project.CCL_ARTIFACT_ID
                version project.CCL_VERSION
                artifact sourcesJar
                artifact bundleReleaseAar
            }
        }
        repositories {
            maven {
                url =  uri("file://${localReleaseDest}")
            }
        }
    }

    task zipRelease(type: Zip) {
        from localReleaseDest
        dependsOn publishLocalLibraryReleasePublicationToMavenRepository
        destinationDir buildDir
        archiveName "release-${project.CCL_VERSION}.zip"
    }

    task generateLibraryRelease {
        dependsOn zipRelease
        doLast {
            println "Release ${project.CCL_VERSION} can be found at ${localReleaseDest}/"
            println "Release ${project.CCL_VERSION} zipped can be found ${buildDir}/release-${project.CCL_VERSION}.zip"
        }
    }
}

artifacts {
    archives sourcesJar
}